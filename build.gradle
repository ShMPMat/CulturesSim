import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.3.0'
    id 'java'
    id 'idea'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io" }
}

sourceSets {
    main {
        kotlin.srcDirs += 'src/'
        java.srcDirs += 'src/'
        resources.srcDirs += 'SupplementFiles'
    }
    test.kotlin.srcDirs += 'test/'
}

dependencies {
    implementation 'com.github.ShMPMat:Random:master-SNAPSHOT' changing true
    implementation 'com.github.ShMPMat:GenerationUtils:master-SNAPSHOT' changing true
    implementation 'org.jetbrains.kotlin:kotlin-reflect:2.3.0'
    implementation project(':EcosystemSimulation')
    implementation project(':WorldviewGenerator')

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'io.mockk:mockk:1.14.9'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'io.tashtabash.visualizer.text.TextCultureVisualizerMainKt'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_25
        languageVersion = KotlinVersion.KOTLIN_2_3
        apiVersion = KotlinVersion.KOTLIN_2_3
    }
}
